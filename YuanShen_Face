Shader "Custom/YuanShen_Face"
{
    Properties
    {
        _BaseMap ("Base Map", 2D) = "white" {}
        _LightMap ("Light Map", 2D) = "white" {}
        _RampMap ("Ramp Map", 2D) = "white" {}

        _ShadowColor ("Shadow Color", Color) = (0.2, 0.2, 0.2, 0.2)

        _BackColor ("Back Color", Color) = (1, 1, 1, 1)

        _OutlineColor ("Outline Color", Color) = (0, 0, 0, 0)
        _OutlineWidth ("Outline Width", Float) = 0.01
        _Factor ("Factor", Range(0, 1)) = 0.5

        _FaceLightmapOffset ("Face Lightmap Offset", Range(-1, 1)) = 0  // 用来对齐明暗

        [KeywordEnum(None,LightMap_R,LightMap_G,LightMap_B,LightMap_A,UV0,UV1,VertexColor,BaseColor,BaseColor_A)]
        _TestMode ("Test Mode", Int) = 0
    }
    SubShader
    {
        Tags { "RenderType"="Opaque" "LightMode"="ForwardBase" "Queue"="Geometry" }
        Cull Off
        LOD 100

        // 正面着色
        Pass
        {
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            #pragma fullforwardshadows
            #pragma multi_compile_fwdbase

            #include "UnityCG.cginc"
            #include "Lighting.cginc"
            #include "UnityGlobalIllumination.cginc"
            #include "AutoLight.cginc"

            float remap(float x, float t1, float t2, float s1, float s2)
            {
                return (x - t1) / (t2 - t1) * (s2 - s1) + s1;
            }

            struct appdata
            {
                float4 vertex : POSITION;
                float2 uv0 : TEXCOORD0;
                float2 uv1 : TEXCOORD1;
                float4 tangent : TANGENT;
                float3 normal : NORMAL;
                float4 vertexColor : Color;
            };

            struct v2f
            {
                float4 pos : SV_POSITION;
                float2 uv0 : TEXCOORD0;
                float2 uv1 : TEXCOORD1;
                float3 normal : TEXCOORD2;
                float3 tangent : TEXCOORD3;
                float3 worldPos : TEXCOORD4;
                float4 vertexColor : TEXCOORD5;
            };

            sampler2D _BaseMap;
            sampler2D _LightMap;
            sampler2D _RampMap;

            float4 _ShadowColor;
            float _FaceLightmapOffset;

            int _TestMode;

            v2f vert (appdata v)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                o.uv0 = v.uv0;
                o.uv1 = v.uv1;
                o.normal = UnityObjectToWorldNormal(v.normal);
                o.tangent = UnityObjectToWorldDir(v.tangent);
                o.worldPos = mul(unity_ObjectToWorld, v.vertex);
                o.vertexColor = v.vertexColor;

                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                float3 normal = normalize(i.normal);
                float3 tangent = normalize(cross(i.normal, i.tangent));
                float3 bitangent = normalize(cross(normal, tangent));
                float3 lightDir = normalize(UnityWorldSpaceLightDir(i.worldPos.xyz));
                float3 viewDir = normalize(UnityWorldSpaceViewDir(i.worldPos.xyz));
                float3 halfDir = normalize(lightDir + viewDir);
                float2 uv0 = i.uv0;
                //uv0.y = 1 - uv0.y;
                float2 uv1 = i.uv1;
                float4 vertexColor = i.vertexColor;

                float HdotV = dot(halfDir, viewDir);
                float NdotV = dot(normal, viewDir);
                float NdotL = dot(normal, lightDir);
                float NdotH = dot(normal, halfDir);

                float4 baseMap = tex2D(_BaseMap, uv0);
                float4 lightMap = tex2D(_LightMap, uv0);

                int mode = 1;
                if (_TestMode == mode++)
                    return lightMap.r;
                if (_TestMode == mode++)
                    return lightMap.g;  // 阴影 Mask
                if (_TestMode == mode++)
                    return lightMap.b;  // 漫反射 Mask
                if (_TestMode == mode++)
                    return lightMap.a;  // 漫反射 Mask
                if (_TestMode == mode++)
                    return float4(uv0, 0, 0);  // UV0
                if (_TestMode == mode++)
                    return float4(uv1, 0, 0);  // UV1
                if (_TestMode == mode++)
                    return vertexColor.xyzz;  // VertexColor
                if (_TestMode == mode++)
                    return baseMap.xyzz;  // BaseColor

                float3 faceLightMap0 = tex2D(_LightMap, float2(uv0.x, uv0.y));
                float3 faceLightMap1 = tex2D(_LightMap, float2(uv0.x + 0.0025, uv0.y));
                float3 faceLightMap2 = tex2D(_LightMap, float2(uv0.x + 0.005, uv0.y));

                float4 right = mul(unity_ObjectToWorld, float4(1, 0, 0, 0));
                float4 up = mul(unity_ObjectToWorld, float4(0, 1, 0, 0));
                float4 front = mul(unity_ObjectToWorld, float4(0, 0, 1, 0));
                float3 left = -right;

                float FdotL = dot(normalize(front.xz), normalize(lightDir.xz));
                float LdotL = dot(normalize(left.xz), normalize(lightDir.xz));
                float RdotL = dot(normalize(right.xz), normalize(lightDir.xz));

                float faceLight0 = faceLightMap0.r + _FaceLightmapOffset;
                float faceLight1 = faceLightMap1.r + _FaceLightmapOffset;
                float faceLight2 = faceLightMap2.r + _FaceLightmapOffset;

                float faceLightRamp0 = (FdotL > 0) * min((faceLight0 > LdotL), (1 > faceLight0 + RdotL));
                float faceLightRamp1 = (FdotL > 0) * min((faceLight1 > LdotL), (1 > faceLight1 + RdotL));
                float faceLightRamp2 = (FdotL > 0) * min((faceLight2 > LdotL), (1 > faceLight2 + RdotL));
                float faceLightRamp = (faceLightRamp0 + faceLightRamp1 + faceLightRamp2) / 3;
                
                float shadowMask = clamp(faceLightRamp, 0.1, 0.9);
                float2 rampUV = float2(shadowMask, 0.5);
                float3 ramp = tex2D(_RampMap, rampUV);
                
                float3 diffuse = lerp(_ShadowColor * baseMap, baseMap, ramp);

                return float4(diffuse, 1);
            }
            ENDCG
        }

        // 背面着色
        Pass
        {
            Cull Front
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag

            #include "UnityCG.cginc"

            struct appdata
            {
                float4 vertex : POSITION;
                float2 uv : TEXCOORD0;
            };

            struct v2f
            {
                float4 pos : SV_POSITION;
                float2 uv : TEXCOORD0;
            };

            sampler2D _BaseMap;
            float4 _BackColor;

            v2f vert (appdata v)
            {
                v2f o;
                o.pos = UnityObjectToClipPos(v.vertex);
                o.uv = v.uv;
                return o;
            }

            fixed4 frag (v2f i) : SV_Target
            {
                float4 baseMap = tex2D(_BaseMap, i.uv);
                float4 col = baseMap * _BackColor;
                return col;
            }
            ENDCG
        }

        // 添加描边
        Pass
        {
            Tags { "LightMode"="Always" }
            Cull Front
            ZWrite On

            CGPROGRAM
            #pragma multi_compile_fog
            #pragma vertex vert
            #pragma fragment frag
            #include "UnityCG.cginc"

            float4 _OutlineColor;
            float _OutlineWidth;
            float _Factor;

            struct v2f
            {
                float4 pos : SV_POSITION;
                UNITY_FOG_COORDS(0)
            };

            v2f vert(appdata_full v)
            {
                v2f o;
                float3 dir0 = normalize(v.vertex.xyz);
                float3 dir1 = v.normal;
                float DdotD = dot(dir0, dir1);
                dir0 *= sign(DdotD);
                dir0 = dir0 * _Factor + dir1 * (1 - _Factor);
                v.vertex.xyz += dir0 * _OutlineWidth * 0.001;
                o.pos = UnityObjectToClipPos(v.vertex);
                UNITY_TRANSFER_FOG(o, o.pos);
                return o;
            }

            float4 frag(v2f i) : COLOR
            {
                float4 col = _OutlineColor;
                UNITY_APPLY_FOG(i.fogCoord, c);
                return col;
            }
            ENDCG
        }
    }
}
